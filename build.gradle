// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    checkstyle {
        toolVersion = '6.12.1'
    }

    checkstyle {
        configFile = rootProject.file('./config/checkstyle.xml')
        configProperties.checkStyleConfigDir = rootProject.rootDir
    }

    tasks.register('checkstyle', Checkstyle) {
        source 'src'
        ignoreFailures = false
        showViolations = true
        include '**/*.java'
        exclude '**/gen/**'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    }

    tasks.register('pmd', Pmd) {
        ignoreFailures = false
        ruleSetFiles = files("${project.rootDir}/config/pmd-ruleset.xml")
        ruleSets = []

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.required.set(false)
            html.required.set(true)
            xml.outputLocation.set(file("$project.buildDir/reports/pmd/pmd.xml"))
            html.outputLocation.set(file("$project.buildDir/reports/pmd/pmd.html"))
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register('check', GradleBuild) {
    tasks = ['test', 'pmd', 'checkstyle']
}
