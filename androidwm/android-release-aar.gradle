//  Run with: ./gradlew publish

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group = properties.getProperty('group')
version = properties.getProperty('version')

apply plugin: 'maven-publish'

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    add('archives', javadocJar)
    add('archives', sourcesJar)
}

publishing {
    publications {
        release(MavenPublication) {
            from components.release
            artifactId = properties.getProperty('artifactId', project.name)
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        def repoUrl = properties.getProperty('maven.repo.url')
        if (repoUrl) {
            maven {
                url = uri(repoUrl)

                def repoUser = properties.getProperty('maven.repo.user')
                def repoPassword = properties.getProperty('maven.repo.password')

                if (repoUser && repoPassword) {
                    credentials {
                        username = repoUser
                        password = repoPassword
                    }
                }
            }
        } else {
            mavenLocal()
        }
    }
}
